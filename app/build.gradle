plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp' version '1.8.0-1.0.8'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    signingConfigs {
        debug {
            storeFile file(localProperties['SIGNING_CONFIG_DEBUG_STORE_FILE'])
            storePassword localProperties['SIGNING_CONFIG_DEBUG_STORE_PWD']
            keyAlias localProperties['SIGNING_CONFIG_DEBUG_KEY_ALIAS']
            keyPassword localProperties['SIGNING_CONFIG_DEBUG_KEY_PWD']
        }
        release {
            storeFile file(localProperties['SIGNING_CONFIG_RELEASE_STORE_FILE'])
            storePassword localProperties['SIGNING_CONFIG_RELEASE_STORE_PWD']
            keyAlias localProperties['SIGNING_CONFIG_RELEASE_KEY_ALIAS']
            keyPassword localProperties['SIGNING_CONFIG_RELEASE_KEY_PWD']
        }
    }
    compileSdk 33

    defaultConfig {
        applicationId "com.example.shoplist"
        minSdk 24
        targetSdk 33
        versionCode 3
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures{
        viewBinding true
    }
    namespace 'com.example.shoplist'

    buildTypes.each {
        it.buildConfigField "String", "BASE_URL", "\"" + localProperties['BASE_URL'] + "\""
    }
}

dependencies {
    //binding
    implementation(libs.viewbinding.delegate)

    //navigation
    implementation(libs.cicerone)

    //koin
    implementation(libs.koin.core)
    implementation(libs.koin.android)

    //androidx
    implementation(libs.androidx.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.constraint)

    //material
    implementation(libs.android.material)

    implementation project(':feature_detail_recipe')
    implementation project(':feature_recipes')
    implementation project(':feature_meal_item')
    implementation project(':feature_favorites')
    implementation project(':feature_settings')
    implementation project(':feature_shoplist')
    implementation project(':data_remote')
    implementation project(':data_local')
    implementation project(':domain')
    implementation project(':Core')

    implementation(libs.firebase.crashlytics.ktx)
    implementation(libs.firebase.analytics.ktx)

    debugImplementation libs.leakcanary.android
}